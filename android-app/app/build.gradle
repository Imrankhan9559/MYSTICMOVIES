import java.util.Properties

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def isWindowsHost = System.getProperty("os.name", "").toLowerCase().contains("windows")

android {
    namespace "com.mysticmovies.app"
    compileSdk 34

    defaultConfig {
        applicationId "com.mysticmovies.app"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "API_BASE_URL", "\"https://mysticmovies.onrender.com\""
        buildConfigField "String", "TELEGRAM_TEST_SHARE_TOKEN", "\"\""
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties["storeFile"])
                storePassword keystoreProperties["storePassword"]
                keyAlias keystoreProperties["keyAlias"]
                keyPassword keystoreProperties["keyPassword"]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        buildConfig true
    }
}

def clearReadOnlyBuildDir = tasks.register("clearReadOnlyBuildDir") {
    doLast {
        if (!isWindowsHost) return
        def buildPath = layout.buildDirectory.get().asFile.absolutePath
        exec {
            commandLine "cmd", "/c", "attrib -R \"${buildPath}\\*\" /S /D"
            ignoreExitValue true
        }
    }
}

tasks.configureEach { task ->
    if (task.name in ["clean", "dexBuilderDebug", "dexBuilderRelease", "mergeProjectDexDebug", "mergeProjectDexRelease"]) {
        task.dependsOn(clearReadOnlyBuildDir)
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.8.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation "io.coil-kt:coil:2.7.0"
    implementation "org.json:json:20240303"
}
